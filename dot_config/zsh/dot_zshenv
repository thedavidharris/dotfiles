# Disable Apple's zsh session logic from /etc/zshrc_Apple_Terminal, this has
# to be done here to load early enough.
export SHELL_SESSIONS_DISABLE=1

# XDG
export XDG_CONFIG_HOME=${XDG_CONFIG_HOME:-$HOME/.config}
export XDG_CACHE_HOME=${XDG_CACHE_HOME:-$HOME/.cache}
export XDG_DATA_HOME=${XDG_DATA_HOME:-$HOME/.local/share}
export XDG_STATE_HOME=${XDG_STATE_HOME:-$HOME/.local/state}
mkdir -p "$XDG_CONFIG_HOME" "$XDG_CACHE_HOME" "$XDG_DATA_HOME" "$XDG_STATE_HOME"

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set up homebrew paths
if [[ $(arch) == arm64* ]]; then
  export BREW_PREFIX=/opt/homebrew
else
  export BREW_PREFIX=/usr/local
fi

# Android
export ANDROID_HOME=$HOME/Library/Android/sdk

# =============================================================================
# PATH CONFIGURATION
# =============================================================================

# Set path with Homebrew
eval "$($BREW_PREFIX/bin/brew shellenv)"

# Build path array (only add directories that exist)
path=(
    $HOME/{,s}bin(/N)                    # User binaries
    $HOME/.local/{,s}bin(/N)             # Local user binaries
    $HOME/.cargo/bin(/N)                 # Rust/Cargo binaries
    $ANDROID_HOME/platform-tools(/N)     # Android tools
    $ANDROID_HOME/tools(/N)              # Android tools
    $ANDROID_HOME/tools/bin(/N)          # Android tools
    $BREW_PREFIX/{,s}bin(/N)             # Homebrew binaries
    /usr/local/{,s}bin(/N)               # System binaries
    /bin                                 # Core system binaries
    $path                                # Existing PATH
)

# 1Password
# This in zsh env because of the way it aliases key commands, although probably should find a way around this
source ~/.config/op/plugins.sh
export BASH_ENV="$XDG_CONFIG_HOME/bash/.bash_env_aliases"
